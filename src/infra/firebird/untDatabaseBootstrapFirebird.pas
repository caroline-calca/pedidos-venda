unit untDatabaseBootstrapFirebird;

interface

uses
  System.SysUtils,
  System.Classes,
  System.IOUtils,
  FireDAC.Comp.Client,
  FireDAC.Stan.Def,
  FireDAC.Phys.FB,
  FireDAC.Phys.FBDef,
  FireDAC.DApt,
  FireDAC.VCLUI.Wait,
  FireDAC.Stan.Param,
  Data.DB,
  Vcl.Controls,

  untUtils,
  untSeedData,
  untDatabaseBootstrap,
  untConnectionManager,
  untConfigManager,
  untCliente,
  untClienteRepository,
  untClienteRepositoryFirebird;

type
  TDatabaseBootstrapFirebird = class(TInterfacedObject, IDatabaseBootstrap)
  private
    FConnBoot: TFDConnection;

    procedure CriarDatabase;
    procedure CriarEstrutura;
    procedure InserirDadosIniciais;
    procedure GarantirEvolucoes;

    procedure InserirClientes;
    procedure InserirProdutos;

    procedure ConectarBoot(const ACreateDatabase: Boolean = False);
  public
    constructor Create;
    destructor Destroy; override;

    function Inicializar: Boolean;
  end;

implementation

{ TDatabaseBootstrapFirebird }

procedure TDatabaseBootstrapFirebird.ConectarBoot(const ACreateDatabase: Boolean = False);
begin
  if FConnBoot.Connected then
    FConnBoot.Connected := False;

  FConnectionManager.ConfigurarParametros(FConnBoot.Params, ACreateDatabase);
  FConnBoot.Connected := True;
end;

constructor TDatabaseBootstrapFirebird.Create;
begin
  //
end;

destructor TDatabaseBootstrapFirebird.Destroy;
begin
  //
  inherited;
end;

procedure TDatabaseBootstrapFirebird.CriarDatabase;
begin
  ConectarBoot(True);
end;

procedure TDatabaseBootstrapFirebird.CriarEstrutura;
var
  Qry: TFDQuery;
begin
  Qry := TFDQuery.Create(nil);
  try
    Qry.Connection := FConnBoot;

    // CLIENTE
    Qry.ExecSQL(
      'CREATE TABLE CLIENTE (' +
      '  CODIGO INTEGER NOT NULL PRIMARY KEY,' +
      '  NOME VARCHAR(100),' +
      '  CIDADE VARCHAR(60),' +
      '  UF CHAR(2)' +
      ')'
    );

    // PRODUTO
    Qry.ExecSQL(
      'CREATE TABLE PRODUTO (' +
      '  CODIGO INTEGER NOT NULL PRIMARY KEY,' +
      '  DESCRICAO VARCHAR(100),' +
      '  PRECO_VENDA NUMERIC(15,2)' +
      ')'
    );

    // PEDIDO
    Qry.ExecSQL(
      'CREATE TABLE PEDIDO (' +
      '  NUMERO_PEDIDO INTEGER NOT NULL PRIMARY KEY,' +
      '  DATA_EMISSAO DATE,' +
      '  CODIGO_CLIENTE INTEGER,' +
      '  VALOR_TOTAL NUMERIC(15,2)' +
      ')'
    );

    // PEDIDO_ITEM
    Qry.ExecSQL(
      'CREATE TABLE PEDIDO_ITEM (' +
      '  ID INTEGER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,' +
      '  NUMERO_PEDIDO INTEGER,' +
      '  CODIGO_PRODUTO INTEGER,' +
      '  QUANTIDADE NUMERIC(15,2),' +
      '  VLR_UNITARIO NUMERIC(15,2),' +
      '  VLR_TOTAL NUMERIC(15,2)' +
      ')'
    );

    // SEQUENCE PEDIDO
    Qry.ExecSQL('CREATE SEQUENCE GEN_PEDIDO');

    // ÍNDICES
    Qry.ExecSQL('CREATE INDEX IDX_PEDIDO_CLIENTE ON PEDIDO (CODIGO_CLIENTE)');
    Qry.ExecSQL('CREATE INDEX IDX_ITEM_PEDIDO ON PEDIDO_ITEM (NUMERO_PEDIDO)');
    Qry.ExecSQL('CREATE INDEX IDX_ITEM_PRODUTO ON PEDIDO_ITEM (CODIGO_PRODUTO)');

    // FK PEDIDO -> CLIENTE
    Qry.ExecSQL(
      'ALTER TABLE PEDIDO ' +
      'ADD CONSTRAINT FK_PEDIDO_CLIENTE ' +
      'FOREIGN KEY (CODIGO_CLIENTE) ' +
      'REFERENCES CLIENTE (CODIGO)'
    );

    // FK ITEM -> PEDIDO
    Qry.ExecSQL(
      'ALTER TABLE PEDIDO_ITEM ' +
      'ADD CONSTRAINT FK_ITEM_PEDIDO ' +
      'FOREIGN KEY (NUMERO_PEDIDO) ' +
      'REFERENCES PEDIDO (NUMERO_PEDIDO) ' +
      'ON DELETE CASCADE'
    );

    // FK ITEM -> PRODUTO
    Qry.ExecSQL(
      'ALTER TABLE PEDIDO_ITEM ' +
      'ADD CONSTRAINT FK_ITEM_PRODUTO ' +
      'FOREIGN KEY (CODIGO_PRODUTO) ' +
      'REFERENCES PRODUTO (CODIGO)'
    );

  finally
    Qry.Free;
  end;
end;

procedure TDatabaseBootstrapFirebird.InserirClientes;
var
  Repo: IClienteRepository;
  Cliente: TCliente;
  I: Integer;
begin
  Repo := TClienteRepositoryFirebird.Create(FConnBoot);

  for I := Low(SEED_CLIENTES) to High(SEED_CLIENTES) do
  begin
    Cliente := TCliente.Criar(I + 1,
                              SEED_CLIENTES[I].Nome,
                              SEED_CLIENTES[I].Cidade,
                              SEED_CLIENTES[I].UF);

    try
      Repo.Inserir(Cliente);
    finally
      Cliente.Free;
    end;
  end;
end;

procedure TDatabaseBootstrapFirebird.InserirProdutos;
var
  Qry: TFDQuery;
  I: Integer;
begin
  Qry := TFDQuery.Create(nil);
  try
    Qry.Connection := FConnBoot;

    // Injeção de PRODUTOS
    for I := 1 to 10 do
    begin
      Qry.SQL.Text :=
        'INSERT INTO PRODUTO (CODIGO, DESCRICAO, PRECO_VENDA) ' +
        'VALUES (:COD, :DESC, :PRECO)';

      Qry.ParamByName('COD').AsInteger := I;
      Qry.ParamByName('DESC').AsString := 'Produto ' + I.ToString;
      Qry.ParamByName('PRECO').AsFloat := 10 * I;

      Qry.ExecSQL;
    end;

  finally
    Qry.Free;
  end;
end;

procedure TDatabaseBootstrapFirebird.InserirDadosIniciais;
begin
  InserirClientes;
  InserirProdutos;
end;

procedure TDatabaseBootstrapFirebird.GarantirEvolucoes;
var
  Qry: TFDQuery;
begin
  Qry := TFDQuery.Create(nil);
  try
    Qry.Connection := FConnBoot;

    // adição de campo OBSERVACAO em PEDIDO
    Qry.SQL.Text :=
      'SELECT 1 ' +
      'FROM RDB$RELATION_FIELDS ' +
      'WHERE RDB$RELATION_NAME = ''PEDIDO'' ' +
      'AND RDB$FIELD_NAME = ''OBSERVACAO''';

    Qry.Open;

    if Qry.IsEmpty then
    begin
      Qry.Close;
      Qry.ExecSQL('ALTER TABLE PEDIDO ADD OBSERVACAO VARCHAR(255)');
    end;
  finally
    Qry.Free;
  end;
end;

function TDatabaseBootstrapFirebird.Inicializar: Boolean;
var
  Config: TConfigManager;
  BancoNovo: Boolean;
begin
  Result := False;

  Config := TConfigManager.Create;
  try
    if not Config.ConfiguracaoValida then
      raise Exception.Create('Configuração de banco inválida.');

    BancoNovo := not FileExists(Config.Database);
  finally
    Config.Free;
  end;

  if BancoNovo then
  begin
    if ShowMsg('O banco de dados configurado não foi encontrado.' + sLineBreak +
               'Deseja criá-lo automaticamente?', mtQuest) <> mrYes then
      Exit(False);
  end;

  FConnBoot := TFDConnection.Create(nil);
  try
    if BancoNovo then
    begin
      CriarDatabase;

      ConectarBoot(False);
      CriarEstrutura;
      InserirDadosIniciais;
    end
    else
      ConectarBoot(False);

    GarantirEvolucoes;

    FConnectionManager.Conectar;

    Result := True;

  finally
    FConnBoot.Connected := False;
    FConnBoot.Free;
  end;

end;

end.
